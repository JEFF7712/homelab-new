{
  config,
  lib,
  pkgs,
  ...
}:
let
  cfg = config.homelab.kiosk;
  kioskBrowser = pkgs.writeShellScript "kiosk-browser" ''
    ${lib.concatMapStringsSep "\n" (
      output: "${pkgs.wlr-randr}/bin/wlr-randr --output ${output} --off || true"
    ) cfg.disableOutputs}
    exec ${pkgs.chromium}/bin/chromium \
      --ozone-platform=wayland \
      --enable-features=UseOzonePlatform \
      --no-first-run \
      --noerrdialogs \
      --disable-infobars \
      --disable-features=Translate,OverlayScrollbar \
      --check-for-update-interval=31536000 \
      --password-store=basic \
      --disable-session-crashed-bubble \
      --remote-debugging-port=9222 \
      --remote-allow-origins=* \
      --force-device-scale-factor=${cfg.scaleFactor} \
      --app="${cfg.url}"
  '';
in
{
  options.homelab.kiosk = {
    enable = lib.mkEnableOption "homelab kiosk display using cage and chromium";

    url = lib.mkOption {
      type = lib.types.str;
      default = "http://10.0.40.12/d/efa86fd1d0c121a26444b636a3f509a8?kiosk";
      description = "URL to display on the kiosk screen";
    };

    drmDevice = lib.mkOption {
      type = lib.types.nullOr lib.types.str;
      default = "/dev/dri/card1";
      description = "DRM device for cage/wlroots to bind to";
    };

    user = lib.mkOption {
      type = lib.types.str;
      default = "kiosk";
      description = "System user to run cage and browser under";
    };

    scaleFactor = lib.mkOption {
      type = lib.types.str;
      default = "1.0";
      description = "Device scale factor for Chromium";
    };

    disableOutputs = lib.mkOption {
      type = lib.types.listOf lib.types.str;
      default = [ ];
      description = "List of output names to disable via wlr-randr before launching Chromium";
    };

    haTokenFile = lib.mkOption {
      type = lib.types.nullOr lib.types.str;
      default = null;
      description = "File holding a Home Assistant long-lived token for the kiosk face. The tmpfs root wipes the Chromium profile every reboot, so when set, a boot service re-seeds the token into the kiosk browser over its local DevTools port. The file must live under /persist and is never part of the store closure.";
    };
  };

  config = lib.mkIf cfg.enable {
    users.users.${cfg.user} = {
      isNormalUser = true;
      uid = 1001;
      group = cfg.user;
      extraGroups = [
        "video"
        "input"
        "audio"
      ];
      createHome = true;
      home = "/home/${cfg.user}";
    };
    users.groups.${cfg.user} = {
      gid = 1001;
    };

    fonts.packages = with pkgs; [
      dejavu_fonts
      freefont_ttf
      noto-fonts
      noto-fonts-color-emoji
    ];

    services.cage = {
      enable = true;
      user = cfg.user;
      extraArguments = [ "-s" ];
      environment = {
        WLR_LIBINPUT_NO_DEVICES = "1";
        WLR_NO_HARDWARE_CURSORS = "1";
        XDG_RUNTIME_DIR = "/run/user/1001";
      }
      // (lib.optionalAttrs (cfg.drmDevice != null) {
        WLR_DRM_DEVICES = cfg.drmDevice;
      });
      program = kioskBrowser;
    };

    systemd.services."cage-tty1" = {
      wantedBy = [ "multi-user.target" ];
      restartIfChanged = lib.mkForce true;
      serviceConfig = {
        Restart = "always";
        RestartSec = "3s";
      };
    };

    systemd.services."kiosk-ha-token-seed" = lib.mkIf (cfg.haTokenFile != null) {
      description = "Seed Home Assistant token into kiosk browser";
      after = [ "cage-tty1.service" ];
      wantedBy = [ "multi-user.target" ];
      restartIfChanged = lib.mkForce true;
      unitConfig.ConditionPathExists = [ cfg.haTokenFile ];
      serviceConfig = {
        Type = "oneshot";
        RemainAfterExit = true;
        Restart = "on-failure";
        RestartSec = "30s";
      };
      path = [ pkgs.python3 ];
      script = ''
        exec ${pkgs.python3}/bin/python3 ${./kiosk-ha-token-seed.py} \
          --token-file ${cfg.haTokenFile}
      '';
    };

    systemd.services."getty@tty1".enable = false;
    systemd.services."autovt@tty1".enable = false;

    boot.kernelParams = [ "consoleblank=0" ];
  };
}
